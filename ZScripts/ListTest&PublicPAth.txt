[VarDef,myList(5),""]
	
[RoutineDef,AdjustPath,//replace backslashes with forward slashes
	[VarDef,pathStr,""]
	[Loop,999,
		[VarSet,in,[StrFind,[StrFromAsc,92],pathStr]]
		[If,[Val,in] > -1,//"
			[VarSet,pathStr,[StrMerge,[StrExtract,pathStr,0,[Val,in-1]],"/",[StrExtract,pathStr,[Val,in+1],[StrLength,pathStr]]]]
			,
			[LoopExit]
		]		
	]
,pathStr]

[RoutineDef,GetFullPath,	
	[VarSet,path,[FileNameResolvePath,path]]
	[If,[ZBrushInfo,6],//is MacOS
		//adjust for Mac OSX
		[VarSet,path,[StrExtract,path,2,256]]
	]
,path]


[IButton,CreateList,"Do it",
	[VarSet,myList(0),"apples"]
	[VarSet,myList(1),"oranges"]
	[VarSet,myList(2),"pears"]
	[VarSet,myList(3),"bananas"]
	[VarSet,myList(4),"lemons"]
	[VarSet,saved,[VarSave,myList,"MySavedList.zvr"]]
	[If,saved,//if value greater than 0
		[Note,"MyList saved!"]
		,
		[Note,"Error - no values saved!"]
	]	
]

[IButton,LoadList,"Do it",	
	[If,[FileExists,"MySavedList.zvr"],
		[VarLoad,myList,"MySavedList.zvr"]
	]
	[Loop,5,
		[Note,[StrMerge,myList([Var,n]),"\n"],,-1]//Note duration of -1 displays with next note
	,n]
	[Note,""]
]





[IButton,GetPublicFolder,"Do it",
	 [VarSet,publicPath,[FileNameExtract,[FileNameResolvePath,"ZPUBLIC_ZPluginData/any.dat"],1]]
	 [RoutineCall,AdjustPath,publicPath]//just as Notes don't display backslashes!
	 [Note,"The ZBrush Data folder path is: \n",,-1]
	 [Note,publicPath,,,,,500]//500 is width
	 [If,[ZBrushInfo,6],//is MacOS
	 	[If,[StrFind,"Shared",publicPath]>-1,
		 	[VarSet,gozPath,[StrExtract,publicPath,0,([StrFind,"Shared",publicPath]+6)]]
		 	[VarSet,gozPath,[StrMerge,gozPath,"Pixologic/GoZProjects/Default/"]]		 	
		 	[Note,"The GoZ Projects path is: \n",,-1]	 	
		 	[Note,gozPath,,,,,500]
		 ]	 	
	 	//note that ZBrush paths on MacOS start with "!:"
	 	//this needs to be removed if passing paths to dynamic libraries etc. - see GetFullPath routine above
	 	,//else Windows
		 [If,[StrFind,"Documents",publicPath]>-1,
		 	[VarSet,gozPath,[StrExtract,publicPath,0,([StrFind,"Documents",publicPath]-1)]]
		 	[VarSet,gozPath,[StrMerge,gozPath,"Pixologic/GoZProjects/Default/"]]
		 	[RoutineCall,AdjustPath,gozPath]
		 	[Note,"The GoZ Projects path is: \n",,-1]	 	
		 	[Note,gozPath,,,,,500]
		 ]
	 ]	
]

